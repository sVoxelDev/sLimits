buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'kr.entree.spigradle' version '2.1.1'
    id 'java'
    id 'jacoco'
    id "org.hibernate.build.maven-repo-auth" version "3.0.2"
    id "io.freefair.lombok" version "5.1.1"
    id 'idea'
}

apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/publish.gradle"

sourceCompatibility = 8
targetCompatibility = 8

ext {
    //Define one of the supported mc versions
    mcVersion = project.property("mcVersion")
    packageName = project.property("packageName")
    guavaVersion = '29.0-jre'
}

group project.property("group")

spigot {
    authors = [project.property("author")]
    apiVersion = project.property("apiVersion")
    load = STARTUP
    //    depends = ['']
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

archivesBaseName = project.property("pluginName")

repositories {
    mavenCentral()
    jcenter()

    //Added spigot repository
    maven {
        name = 'Spigot'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }

    maven {
        name = 'slib'
        url = 'https://maven.pkg.github.com/silthus/slib'
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }

    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/central' }

    //Add your repositories here
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url 'https://jitpack.io' }

    maven { url = 'https://repo.codemc.org/repository/maven-public' }
}


dependencies {
    //Adds the spigot api to your plugin
    compileOnly "org.spigotmc:spigot-api:${mcVersion}-R0.1-SNAPSHOT"
    compileOnly 'org.yaml:snakeyaml:1.26'

    //Add your dependencies here
    implementation 'net.silthus:slib:1.5.1'
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
    implementation 'com.github.stefvanschie.inventoryframework:IF:0.5.19'
    implementation 'org.bstats:bstats-bukkit:1.7'

    //Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
    testImplementation "org.mockito:mockito-core:3.+"
    testImplementation mockBukkit()
    testImplementation 'org.assertj:assertj-core:3.16.1'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    ignoreFailures = true
}

shadowJar {
    classifier = ''
    dependencies {
        include(dependency('de.exlll:configlib-bukkit:'))
        include(dependency('co.aikar:acf-paper:'))
        include(dependency('com.github.stefvanschie.inventoryframework:'))
        include(dependency('net.silthus:slib:'))
        include(dependency('org.bstats::'))
    }
    relocate 'co.aikar.commands', "${packageName}.acf"
    relocate 'co.aikar.locales', "${packageName}.locales"
    relocate 'de.exlll.configlib', "${packageName}.configlib"
    relocate 'com.github.stefvanschie.inventoryframework', "${packageName}.inventoryframework"
    relocate 'net.silthus.slib', "${packageName}.slib"
    relocate 'org.bstats', "${packageName}.bstats"
}

tasks.build.dependsOn(shadowJar)
tasks.publish.dependsOn(shadowJar)
tasks.prepareSpigotPlugins.dependsOn(shadowJar)

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    ignoreFailures = false
}

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

defaultTasks 'build'